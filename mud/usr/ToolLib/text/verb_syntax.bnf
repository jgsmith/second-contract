# grammar for parsing syntax patterns
#
# things like "<direct:object>"

whitespace = /[ \b]+/

_word = /[a-z]+/
qstring = /'[^']*'/

syntax : bit
syntax : syntax bit

bit : word
bit : '<' slot '>' ? _slot
bit : '<' slot qstring '>' ? _slot_qstring
bit : '{' word_list '}' 
bit : '{' word_list qstring '}'
bit : '[' word_list ']'

slot : direct_object
slot : indirect_object
slot : _word

word : _word
word : 'direct'
word : 'indirect'
word : 'direct-obs'
word : distance
word : location
word : word '-' word

word_list : word
word_list : word_list '|' word

direct_object : 'direct' ? _direct
direct_object : 'direct' ':' object_type ? _direct_obj
direct_object : 'direct' ':' object_type ':' direct_env ? _direct_obj_env

indirect_object : 'indirect' ? _indirect
indirect_object : 'indirect' ':' object_type ? indirect_obj
indirect_object : 'indirect' ':' object_type ':' indirect_env ? _indirect_obj_env

#
# details can be objects, but they are not manipulable. If a verb requires
# that the item be movable or otherwise detachable from its environmental
# relationships, then the verb should use object. Otherwise, 'detail' will
# match anything that has the 'detail:...' properties.
#
# So, we can have "look <direct:detail:backdrop-me>" to match any detail
# in an object in the current scene. But we might want to use
# "examine <direct:detail:near-me>" to restrict close examination to anything
# in "arms-reach".
#
# To reach any player in the world: "<direct:player:backdrop-world>"
#
object_type : 'detail'
object_type : 'object'
object_type : 'living'
object_type : 'player'
object_type : 'details'
object_type : 'objects'
object_type : 'livings'
object_type : 'players'

distance : 'in'
distance : 'on'
distance : 'close'
distance : 'near'
distance : 'vicinity'
distance : 'far'
distance : 'backdrop'

location : 'me'
location : 'scene'
location : 'area'
location : 'region'
location : 'world'

direct_env : distance '-' location ? _concat

# any of the direct object environments
indirect_env : direct_env

# or in relation to the direct objects
indirect_env : distance '-' 'direct-obs' ? _concat
